import win.ui;
import web.json;
import win.clip;
import inet.http;
import web.layout;
import web.layout.debug; 
import web.layout.behavior.windowCommand; 

var WIN_WIDTH  = 498;
var WIN_HEIGHT = 495;
var createForm  = ..win.form( right=WIN_WIDTH;bottom=WIN_HEIGHT;parent=...;border="none";scroll="none";text="ÐÂ½¨")

createLayout = web.layout(createForm)  
createLayout.go("/layout/create.html") 
createLayout.queryEle("#typeBox th").innerText    = lang.create.objectType;
createLayout.queryEle("#productBox th").innerText = lang.create.product;
createLayout.queryEle("#projectBox th").innerText = lang.create.project;
createLayout.queryEle("#titleBox th").innerText   = lang.create.title;
createLayout.queryEle("#descBox th").innerText    = lang.create.desc;

..http = inet.http();
function loadProducts()
{
	var response = http.get(getAPI({"api"; "getmodel"; "product"; "getPairs"}));
	response = web.json.parse(response);
	response.data = web.json.parse(response.data);
	var products = "";
	for(productID,productName in response.data)
	{
		productName = string.replace(productName, "\\", "");
		products += "<option value='" + productID + "'>" + productName + "</option>";
	}
	createLayout.queryEle("#productBox").style.display = "block";
	createLayout.queryEle("#products").innerHTML = products;
}

function loadProjects()
{
	var response = http.get(getAPI({"api"; "getmodel"; "project"; "getPairs"}));
	response = web.json.parse(response);
	response.data = web.json.parse(response.data);
	var projects = "";
	for(projectID,projectName in response.data)
	{
		projectName = string.replace(projectName, "\\", "");
		projects += "<option value='" + projectID + "'>" + projectName + "</option>";
	}
	createLayout.queryEle("#projectBox").style.display = "block";
	createLayout.queryEle("#projects").innerHTML = projects;
}

selectListener = createLayout.queryEle("#objectType");
selectListener.onSelectSelectionChanged = function( ltTarget,ltEle,x,y,mouseParams ) 
{
	objectType = ltTarget.value;
	if(objectType == 'story' || objectType == 'bug') 
	{
		createLayout.queryEle("#projectBox").style.display = "none";
		return loadProducts();
	}
	if(objectType == 'task') 
	{
		createLayout.queryEle("#productBox").style.display = "none";
	 	return loadProjects();
	}
	if(objectType == 'todo') 
	{ 
		createLayout.queryEle("#productBox").style.display = "none";
		createLayout.queryEle("#projectBox").style.display = "none";
	}
}
selectListener.attachEventHandler();

function showResult(result, output)
{
	if(result == 'success') output = "<span style='color:green'>" + output + "</span>";
	if(result == 'failed')  output = "<span style='color:red'>" + output + "</span>";
	createLayout.queryEle("#result").innerHTML = output;
	createForm.setTimeout(function(){createLayout.queryEle("#result").innerHTML = "";}, 5000); 
}

function submit(objectType, product, project, name, desc)
{
	encodedName = inet.url.encode(name, true);
	encodedDesc = inet.url.encode(desc, true);
	
	var post = "";
	var postApi = getAPI({"api"; "getmodel"; objectType; "create"; viewType='html'});
	select(objectType)
	{
		case "todo"  {post = string.concat("type=custom&name=", encodedName, "&desc=", encodedDesc);}
		case "story" {post = string.concat("product=", product, "&title=", encodedName, "&spec=", encodedDesc);}
		case "task"  
		{
			post = string.concat("type=design&assignedTo%5B%5D=", "&name=", encodedName, "&desc=", encodedDesc);
			postApi = getAPI({"api"; "getmodel"; objectType; "create"; "projectID=" + project; viewType='html'})
		}
		case "bug"   {post = string.concat("openedBuild%5B%5D=Trunk&product=", product, "&title=", encodedName, "&steps=", encodedDesc);}
	}

	response = http.post(postApi, post);
	response = web.json.parse(response);
	if(response.status == 'success') 
	{
		if(objectType == 'task') 
		{
			response.data = web.json.parse(response.data);
			objectID = response.data[''];
		}
		else 
		{
			objectID = string.replace(response.data, """", "");
		}
		
		return showResult("success", lang.create.success + objectType + "#" + objectID + " " + name);
	}
	return showResult("failed", lang.create.failed);
}

submitListener = createLayout.queryEle("input[type='submit']");
submitListener.onButtonClick = function (ltTarget,ltEle,reason,behaviorParams) {
	var name = createLayout.queryEle("#name").value;
	if(name == '') 
	{ 
		showResult("failed", string.format(lang.prompt.notEmpty, lang.create.title));
		return; 
	}
	var objectType = createLayout.queryEle("#objectType").value;
	var product    = createLayout.queryEle("#productBox select").value;
	var project    = createLayout.queryEle("#projectBox select").value
	var desc       = createLayout.queryEle("#desc").value;
	submit(objectType, product, project, name ,desc);	
}
submitListener.attachEventHandler();

createLayout.queryEle("textarea").value = win.clip.read() ? win.clip.read() : "";

createForm.show();
win.loopMessage();
return createForm;
