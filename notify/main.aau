import win.ui;
import web.layout;
import web.layout.behavior.windowSizer;
import web.layout.behavior.windowCommand;
import web.layout.behavior.keyboard;
import win.util.round;
import string.md5
import inet.http;
import web.json;

var loginform = ..win.form( bgcolor=16777215;bottom=696;parent=...;right=318;border="none";text="禅道桌面提醒工具" )
win.util.round(loginform)
wbLayout = web.layout(loginform)  
wbLayout.go("/layout/login.html") 

wbLayout.onButtonClick = 
{
	login = function(ltTarget,ltEle,x,y,ltMousePara)
	{	
		..user = {}
		user.zentaourl = wbLayout.getEle("zentaourl").value;
		user.account   = wbLayout.getEle("account").value;
		user.password  = wbLayout.getEle("password").value;
		if(#user.password != 32) user.password = string.md5(user.password, true);
		if(!init()) return;
		indexFile = loadcode("../index.aau");	
		indexFile("../index");
		loginform.close();
	}
}


..http = inet.http();
now = time();
now.format="%Y-%m-%d";
var LOG_FILE  = "../log/" + tostring(now) + '.txt';
var INFO_FILE = "../tmp/.logininfo";

langFile = loadcode("../lang/zh-cn.aau");
langFile("../lang/zh-cn");
configFile = loadcode("../config/config.aau");
configFile("../config/config.aau");

function loadUserInfo()
{
	var userInfo = string.load(INFO_FILE);
	if(!userInfo) return;
	userInfo = web.json.parse(userInfo);
	wbLayout.getEle("zentaourl").value = userInfo.zentaourl;
	wbLayout.getEle("account").value   = userInfo.account;
	wbLayout.getEle("password").value  = userInfo.password;	
}
loadUserInfo();

function log(content)
{
	string.save(LOG_FILE, content + '\n', true);
}

function getAPI(param)
{
	var moduleName = param[1]
	var methodName = param[2]
	var api = ''
	var viewType = param.viewType ? param.viewType : "json"; 
    if(zentaoConfig.requestType == 'GET')
	{
		
	}
	else 
	{
		api = user.zentaourl + "/"
		if(moduleName == 'user' and methodName == 'login')
		{
			api += "user-login.json?account=" + user.account + "&password=" + user.password; 
			log(api)
		 	return api;
		 }

		for(i=1; #param; 1) 
		{
			api += param[i]; 
			if(param[i+1]) api += "-";
		}
		if(param.pageID)
		{ 
			if(methodName == 'todo') api += "--all-date_desc,status,begin-";
			if(methodName != 'todo') api += "-id_desc-";
			api += pager.recTotal + "-" + pager.recPerPage + "-" + param.pageID;
		}
		api += "." + viewType;
		if(session) api += "?" + session.data.sessionName + "=" + session.data.sessionID;
	}
	log(api)
	return api;
}

function getConfig()
{
	zentaoConfig = http.get(user.zentaourl + "/index.php?mode=getconfig");
	if(!zentaoConfig) return false;
	zentaoConfig = web.json.parse(zentaoConfig); 
	return true;
}

function getSession()
{
	session = http.get(getAPI({'api'; 'getsessionid'}));
	if(!session or session == '') return false;
	session = web.json.parse(session);
	session.data = web.json.parse(session.data);
	user.password = string.md5(user.password + tostring(session.data.rand), true);	
	return true;
}

function login()
{
	var response = http.get(getAPI({'user'; 'login'}));
	response = web.json.parse(response);
	if(response.status == 'failed') return false;
	return true;
}

function getRole()
{
	var response = http.get(getAPI({"api"; "getmodel"; "user"; "getById"; "account=" + user.account}));
	if(!response or response == '') return false;
	response = web.json.parse(response);
	response = web.json.parse(response.data);
	user.role = response.role;
	return true;
}

function init()
{
	if(!getConfig())  {win.msgbox(lang.prompt.wrongUrl, lang.prompt.warning); return false} 
	if(!getSession()) {win.msgbox(lang.prompt.wrongUrl, lang.prompt.warning); return false} 
	if(!login())      {win.msgbox(lang.prompt.wrongInfo, lang.prompt.warning); return false} 
	if(!getRole())    {win.msgbox(lang.prompt.wrongInfo, lang.prompt.warning); return false}
	string.save(INFO_FILE, web.json.stringify(user, true));
	return true;
}

loginform.show() 
win.loopMessage();
return loginform;
